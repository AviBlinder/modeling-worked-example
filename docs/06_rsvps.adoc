= RSVPs
:csv-url: https://raw.githubusercontent.com/neo4j-meetups/modeling-worked-example/master/data/
:icons: font

== RSVPs

To make location based recommendations we need to know where people have previously attended events and luckily the meetup.com API gives us RSVPs which can serve as a proxy for attendance.

Let's see what data we've got to work with:

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS
FROM "{csv-url}rsvps.csv" AS row
RETURN row
LIMIT 10
----

So we get an identifier for each RSVP, an event id which the RSVP is for, a member id that identifies the user and a few other attributes.
Let's get the RSVPs into the graph then!

== Importing RSVPs

We're only going to worry ourselves with 'yes' RSVPs, we won't import any 'no' or 'waitlist' responses.

[source,cypher,subs=attributes]
----
USING PERIODIC COMMIT 10000
LOAD CSV WITH HEADERS FROM "{csv-url}rsvps.csv" AS row
WITH row WHERE row.response = "yes"

MATCH (member:Member {id: row.member_id})
MATCH (event:Event {id: row.event_id})
MERGE (member)-[rsvp:RSVPD {id: row.rsvp_id}]->(event)
ON CREATE SET rsvp.created = toint(row.created),
              rsvp.lastModified = toint(row.mtime),
              rsvp.guests = toint(row.guests)
----

Let's check they got imported correctly:

[source,cypher]
----
MATCH (event:Event {name: "GraphHack @ Graph Connect 2016"})<-[:RSVPD]-(attendee)
RETURN attendee
LIMIT 10
----

[source,cypher]
----
MATCH (event:Event {name: "GraphHack @ Graph Connect 2016"})<-[:RSVPD]-(attendee)
RETURN count(*)
----


ifdef::env-training[]
This query shows us who's signed up for the hackathon we're running tonight - hopefully you can see your name on that list?!

Now we're ready to make use of RSVPs and venues in our events recommendation query.


== Exercise: Events at my venues

Our initial event recommendation query only recommends events for the groups that we belong to.
Let's now update the query to:

* include events at venues that I've previously visited
* include events within 1km of the top 5 venues that I've previously visited

== To the slides!

image::{img}/slides.jpg[]

endif::env-training[]

== Next Step
In our next section we're going to take a brief detour to introduce procedures, a new addition in Neo4j 3.0.0.
We'll then make use of them in our recommendations and even write some of our own if time permits.

pass:a[<a play-topic='{guides}/07_procedures.html'>Procedures</a>]
