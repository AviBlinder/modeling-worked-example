= Latent Social Graph
:csv-url: https://raw.githubusercontent.com/neo4j-meetups/modeling-worked-example/master/data/
:icons: font

== Find the social graph lurking within

The social graph is a common feature of a lot of modern web applications.
Typically that social graph is imported from a 3rd party system e.g. Twitter followers or Facebook friends.

In this dataset there is actually a latent social graph hiding beneath the data we've looked at so far.
RSVPs give us a reasonable idea of which people may know each other based on common meetups they've attended.

We can write the following query to see which people on the meetup scene that I may know:

[source,cypher]
----
MATCH (me:Member {name: "Mark Needham"})-[:RSVPD]->()<-[:RSVPD]-(otherPerson)
RETURN otherPerson, COUNT(*) AS commonEvents
ORDER BY commonEvents DESC
LIMIT 10
----

== Materialising the latent social graph

In this next section we're going to materialise this inferred social network and create a `FRIENDS` relationship between people who have been to 5 or more of the same events.

We'll have to run a graph global query to materialise this inferred social network so we're going to process members in batches.

First we'll add a `Process` label to active members so we know which ones we've still got left to process in the next step.
Run the following query.

[source,cypher]
----
MATCH (m:Member) WHERE size( (m)-[:RSVPD]->() ) >= 5
SET m:Process
RETURN count(*)
----

So we should have just over 9,000 members to process.

== Transaction State

image::{img}/slides.jpg[]

== Materialising the latent social graph

Let's try processing just the first 1,000 of those members.
Run the following query:

[source,cypher]
----
MATCH (m1:Process) WITH m1 LIMIT 1000
REMOVE m1:Process
WITH m1
MATCH (m1)-[:RSVPD]->(event)<-[:RSVPD]-(m2)

WITH m1, m2, COUNT(*) AS times
WHERE times >= 5

MERGE (m1)-[:FRIENDS]-(m2)
RETURN count(*);
----

You should see that ~ 63,000 relationships have been created.

It would be nice if we could execute that query multiple times until all members have been processed.

== Materialising the latent social graph

The `apoc.periodic.commit(statement,params)` stored procedure allows us to do just that.
Run the following query but be patient, it's got a lot of friendships to make!

[source,cypher]
----
call apoc.periodic.commit("
  MATCH (m1:Process) WITH m1 LIMIT {limit} REMOVE m1:Process
  WITH m1 MATCH (m1)-[:RSVPD]->(event)<-[:RSVPD]-(m2)
  WITH m1, m2, COUNT(*) AS times WHERE times >= 5
  MERGE (m1)-[:FRIENDS]-(m2) RETURN count(*)
",{limit:1000})
----

This query will probably run for a couple of minutes, creating ~ 300,000 `FRIENDS` relationships.

Run the following query to check that they've been created:

[source,cypher]
----
MATCH ()-[:FRIENDS]-() RETURN COUNT(*)
----

== Exercise: Adding friends to the recommendations query

Update the events recommendation query to:

* count how many of our friends have `RSVPD` for an event
* return the names of those friends as well

== Next Step
In our next section we're going to apply some more sophisticated scoring functions to the recommendation queries that we've built so far.

pass:a[<a play-topic='{guides}/09_scoring.html'>Scoring</a>]
