= Recommendations by implicit interest
:csv-url: https://raw.githubusercontent.com/neo4j-meetups/modeling-worked-example/master/data/
:icons: font

== Exclude groups I’m a member of

Finding groups similar to the Neo4j group is a decent start but most good recommendation engines try to make recommendations customised for the user.

Although our first query was a good one to get us started, unfortunately it was suggesting that we join groups that we might already be a member of.
The worst type of recommendation!

In this section we're going to add membership data to our model to fix that.

image::{img}/group_has_topic_member_of.png[]

[verse]
____
As a member of the Neo4j London group
I want to find other similar meetup groups that I’m not already a member of
So that I can join those groups
____

== Explore Members data

Let's see what data we've got to play with.
Run the following query to show the first ten rows of the members CSV file:

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS
FROM "{csv-url}members.csv" AS row
RETURN row
LIMIT 10
----

We've got a member id and name as well as a group id and join date for that group.
You'll also have noticed a `;` separated list of topic ids - we'll look at how to handle that in a few sections time.

== Add Members

Before we run our query to import members we'll create a unique constraint on `Member(id)` to ensure we don't insert the same member twice.

[source,cypher,subs=attributes]
----
CREATE CONSTRAINT ON (m:Member)
ASSERT m.id IS UNIQUE
----

The CSV file contains one entry for each group a member is part of so we will iterate over some members multiple times.
We've got a lot of members to process so we're going to use the `PERIODIC COMMIT` clause to process them in batches.

== Look at the slides to reveal all about periodic commit

image::{img}/slides.jpg[]

== Add Members

Run the following query to import members into the graph:

[source,cypher,subs=attributes]
----
USING PERIODIC COMMIT 10000
LOAD CSV WITH HEADERS
FROM "{csv-url}members.csv" AS row
WITH DISTINCT row.id AS id, row.name AS name
MERGE (member:Member {id: id})
ON CREATE SET member.name = name
----

The use of periodic commit means that we'll commit every 10,000 rows rather than processing the whole file in one large transaction.

== Connect members and groups

Now that we've got the members imported, the next step is to get them connected to the groups that we loaded in the first section.

Run the following query to create a `MEMBER_OF` relationship between members and groups:

[source,cypher,subs=attributes]
----
USING PERIODIC COMMIT 10000
LOAD CSV WITH HEADERS
FROM "{csv-url}members.csv" AS row
WITH row WHERE NOT row.joined is null
MATCH (member:Member {id: row.id})
MATCH (group:Group {id: row.groupId})
MERGE (member)-[membership:MEMBER_OF]->(group)
ON CREATE SET membership.joined=toInt(row.joined);
----

Let's see what our graph looks like now:

[source,cypher,subs=attributes]
----
MATCH (member:Member)-[membership:MEMBER_OF]->(group)
RETURN member, group, membership
LIMIT 10
----

== Look at the slides for a quick explanation of `WITH`

image::{img}/slides.jpg[]

== Additional indexes

We're going to search for members by name so we'll put an index on `Member(name)` so we can quickly do that.

[source,cypher,subs=attributes]
----
CREATE INDEX ON :Member(name)
----

Now it's your turn!

== Exercise: Find yourself and your groups

We've now got groups, topics and members loaded into our database so it's time for a bit more exploration.

* If you're from London write a query to find yourself in the database. If not try and find your neighbour.
* How many groups are you a member of?
* Which topics do those groups have?
* _(For bonus points)_ Which topic shows up the most?

== All the answers will be revealed...just as soon as you look at the slides

image::{img}/slides.jpg[]

== Exclude groups I’m a member of

For our next recommendation we're still going to be finding similar groups to the Neo4j one but we'll also check whether we're already a member of the other groups.

[source,cypher,subs=attributes]
----
MATCH (member:Member {name: "Mark Needham"})
MATCH (group:Group {name: "Neo4j - London User Group"})-[:HAS_TOPIC]->(topic)<-[:HAS_TOPIC]-(otherGroup:Group)
RETURN otherGroup.name,
       COUNT(topic) AS topicsInCommon,
       EXISTS((member)-[:MEMBER_OF]->(otherGroup)) AS alreadyMember,
       COLLECT(topic.name) AS topics
ORDER BY topicsInCommon DESC
LIMIT 10
----

So now we identify them as being ones we’re already a member of so there’s no point in recommending them again.

An interesting thing to notice is that we now get back groups which we didn’t before.
That’s because we didn’t force any ordering other than `topicsInCommon` so we won't get a repeatable order on other columns.

Try changing the member name to your name or your neighbours and see how the results change.

* INSERT SOME OTHER NAMES THAT PEOPLE CAN TRY HERE *

== Exclude groups I’m a member of

In the following query we move the exclusion pattern up into a `WHERE` clause so groups we're already a member of won't be returned at all.

[source,cypher,subs=attributes]
----
MATCH (member:Member {name: "Mark Needham"})
MATCH (group:Group {name: "Neo4j - London User Group"})-[:HAS_TOPIC]->(topic)<-[:HAS_TOPIC]-(otherGroup:Group)
WHERE NOT((member)-[:MEMBER_OF]->(otherGroup) )
RETURN otherGroup.name,
       COUNT(topic) AS topicsInCommon,
       COLLECT(topic.name) AS topics
ORDER BY topicsInCommon DESC
LIMIT 10
----

Try changing the names of the group and member to see how the results vary.

== Find my similar groups

Now that we've got the data loaded in we can start making recommendations on an individual basis.

image::{img}/group_has_topic_member_of_interested_in.png[]

[verse]
____
As a member of several meetup groups
I want to find other similar meetup groups that I’m not already a member of
So that I can join those groups
____

We can use collaborative filtering to see what other groups people in our groups join.

A classic case of *closing the triangle.*
We have two sides, let’s close the third side.

If I join groups which have a specific topic more frequently then we can weight in that ones favour.

== Next Step

Looking at our interests we can determine new interesting groups as well as infer new interests based on my membership and attendance.

pass:a[<a play-topic='{guides}/03_my_interests.html'>My Interests</a>]
