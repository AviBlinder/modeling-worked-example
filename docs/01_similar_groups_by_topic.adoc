= Similar groups
:csv-url: https://raw.githubusercontent.com/neo4j-meetups/modeling-worked-example/master/data/
:icons: font

== Find similar groups to Neo4j

image::{img}/group_has_topic.png[float=right]

[verse]
____
As a member of the Neo4j London group
I want to find other similar meetup groups
So that I can join those groups
____

== What makes groups similar?

One thing that makes groups similar is the topics that they’re about.

[verse]
____
As a member of the Neo4j London group
I want to find other similar meetup groups
So that I can join those groups
____

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS
FROM "{csv-url}groups.csv"
AS row
RETURN row
LIMIT 10
----

== Import Groups

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS
FROM "{csv-url}groups.csv"
AS row
CREATE (:Group { id:row.id,
                 name:row.name,
                 urlname:row.urlname,
                 rating:toInt(row.rating),
                 created:toInt(row.created) })
----

We use CREATE because we know the database is empty when we start.

[source,cypher,subs=attributes]
----
MATCH (g:Group)
RETURN g.id, g.name, g.urlname
----

Topics express areas of interest for both groups and members.

== Detour: MERGE & Constraints

We want to use `MERGE` because we want to avoid creating duplicate topics
We'll put a unique constraint on `:Topic(id)` so that we don’t create the same topic twice.

Unique constraints:

* ensure uniqueness of a (label, property) pair
* allow fast lookup of nodes which match these (label,property) pairs by creating an index on the (label, property) pair.

[source,cypher,subs=attributes]
----
CREATE CONSTRAINT ON (t:Topic) ASSERT t.id IS UNIQUE
----

[source,cypher,subs=attributes]
----
CREATE CONSTRAINT ON (g:Group) ASSERT g.id IS UNIQUE
----

== Import Topics

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}groups_topics.csv"  AS row
MERGE (topic:Topic {id: row.id})
ON CREATE SET topic.name = row.name,
              topic.urlkey = row.urlkey
----

=== Find Topics

[source,cypher,subs=attributes]
----
MATCH (t:Topic)
RETURN t.id, t.name
----

== Connect groups and topics

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}groups_topics.csv"  AS row
MATCH (topic:Topic {id: row.id})
MATCH (group:Group {id: row.groupId})
MERGE (group)-[:HAS_TOPIC]->(topic)
----

The use of `MERGE` here ensures that we end up with a single unique link between a group and a topic.

If we run the query a second time nothing will happen.
If we run another `MERGE` query that tries to create a `:HAS_TOPIC` relationship when one already exists it won’t do anything.

== Additional Indexes

We create an index on `:Group(name)` so that we can quickly look up groups by name.

[source,cypher,subs=attributes]
----
CREATE INDEX ON :Group(name)
----

Same for topics.

[source,cypher,subs=attributes]
----
CREATE INDEX ON :Topic(name)
----

== Exercise: Explore the graph

We've now loaded groups and topics but we don't know exactly what's in our graph so let's do some exploration.

* What's the most popular topic?
* Which group was created most recently?
* How many groups have been running for 4 years or more?

_Hint:_ The link:http://neo4j.com/docs/milestone/cypher-refcard/[Cypher refcard] will come in handy for syntax we haven't covered yet!

==  Find similar groups to Neo4j

So you've hopefully now got an idea of what the data looks like.
It's time to write our first recommendation query which will find groups that have the same topics as the Neo4j London group:

[source,cypher,subs=attributes]
----
MATCH (group:Group {name: "Neo4j - London User Group"})-[:HAS_TOPIC]->(topic)<-[:HAS_TOPIC]-(otherGroup)
RETURN otherGroup.name, COUNT(topic) AS topicsInCommon,
       COLLECT(topic.name) AS topics
ORDER BY topicsInCommon DESC, otherGroup.name
LIMIT 10
----

This query

* starts from the Neo4j group,
* finds its topics,
* then looks for other groups that have those topics
* and aggregates the groups with the most topics in common.

Try changing the group name e.g. `Big Data Debate` or `Docker London` and see how the results change.

== Next Step

Let's look at our groups and how we can use that related information to recommend new groups for ourselves.

pass:a[<a play-topic='{guides}/02_my_similar_groups.html'>Groups similar to mine</a>]
