= Events
:csv-url: https://raw.githubusercontent.com/neo4j-meetups/modeling-worked-example/master/data/
:icons: font

== Events

So far we've only made recommendations for meetup groups that we might like to join.
It's been fun but things start getting much more interesting once we bring events into the equation.

We'll start with exploration of the events CSV file:

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}events.csv" AS row
RETURN row
LIMIT 5
----

As you can see we've got an event id, name, time, and description as well as some ids that reference other entities.

== Import Events

First a bit of preparatory work before we import the events:

[source,cypher,subs=attributes]
----
CREATE INDEX ON :Event(id)
----

[source,cypher,subs=attributes]
----
CREATE INDEX ON :Event(time)
----

Now we're all set to import the events into the graph:

== Import Events

Run the following query to import the events:

[source,cypher,subs=attributes]
----
USING PERIODIC COMMIT 10000
LOAD CSV WITH HEADERS FROM "{csv-url}events.csv" AS row
MERGE (event:Event {id: row.id})
ON CREATE SET event.name = row.name,
              event.time = toInt(row.time),
              event.utcOffset = toInt(row.utc_offset)
----

Let's check that the events imported correctly:

[source,cypher,subs=attributes]
----
MATCH (event:Event)
RETURN event
LIMIT 10
----

Looks good!

== Connect events and groups

Our events are floating around not connected to anything so let's resolve that.
We'll use the `groupId` field from the CSV file to lookup a group that we created earlier and create a relationship between the event and group:

[source,cypher,subs=attributes]
----
USING PERIODIC COMMIT 10000
LOAD CSV WITH HEADERS FROM "{csv-url}events.csv" AS row

WITH distinct row.group_id as groupId, row.id as eventId
MATCH (group:Group {id: groupId})
MATCH (event:Event {id: eventId})
MERGE (group)-[:HOSTED_EVENT]->(event)
----

Run the following query to check that worked as expected:

[source,cypher,subs=attributes]
----
MATCH (group:Group)-[:HOSTED_EVENT]->(event)
WHERE group.name STARTS WITH "Neo4j" AND event.time < timestamp()
RETURN event
ORDER BY event.time DESC
----

If all's well you should see the last 10 events hosted by the Neo4j London group.

== Find future events in my groups

The simplest event recommendation we can make is to find the groups weâ€™re a member of and then find its future events.
We'll start with that and build from there:

[source,cypher,subs=attributes]
----
MATCH (member:Member {name: "Mark Needham"})-[:MEMBER_OF]->(group)-[:HOSTED_EVENT]->(futureEvent)
WHERE futureEvent.time > timestamp()
RETURN group.name,
       futureEvent.name,
       round((futureEvent.time - timestamp()) / (24.0*60*60*1000)) AS days
ORDER BY days
----

That's a good start but it's a bit restrictive - I'm never going to learn about events in groups that I haven't joined yet.

== Find future events for my topics

One way we could make a better recommendation is to suggest events hosted by groups that have my topics even if I'm not a member of those groups.

[source,cypher,subs=attributes]
----
MATCH (member:Member {name: "Mark Needham"})-[:MEMBER_OF]->(group)-[:HOSTED_EVENT]->(futureEvent)
WHERE futureEvent.time > timestamp()
WITH member,
     COLLECT({group: group, myGroup: true, event: futureEvent}) AS myGroupEvents

MATCH (member)-[:INTERESTED_IN]->()<-[:HAS_TOPIC]-(group)-[:HOSTED_EVENT]->(futureEvent)
WHERE futureEvent.time > timestamp()

WITH member, myGroupEvents,
     COLLECT({group: group, myGroup: EXISTS((member)-[:MEMBER_OF]-(group)), event: futureEvent}) AS myTopicEvents

UNWIND myGroupEvents + myTopicEvents AS row

WITH row.event AS event, row.group AS group, row.myGroup AS myGroup, COUNT(*) AS times

WITH event, group, CASE WHEN myGroup THEN 5 ELSE 0 END AS myGroupScore, times

RETURN row.event.name, row.event.time, row.group.name, myGroupScore + times AS score
ORDER BY score DESC
----

* THIS NEEDS SOME WORK - I'll come back to it*

== Exercise: Extending events recommendation

Update the recommendations query to:

* Only show events happening in the next 7 days
* Give a higher score to events in my groups

* WILL THINK OF SOMETHING BETTER TO GO HERE*

== You know the drill by now!

image::{img}/slides.jpg[]


== Next Step

Next we're going to introduce venues into the model so we can make recommendations based on where events are being held.

pass:a[<a play-topic='{guides}/05_venues.html'>VenuesRecommendations</a>]
