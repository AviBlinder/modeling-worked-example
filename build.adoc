= Modeling a recommendation engine using Neo4j

In this session we're going to build a meetup recommendation engine using data extracted from the link:http://www.meetup.com/api[meetup.com] API.

The goal is to get to the point where we've designed an application which can provide a multi faceted recommendation.

Some of the things we'll take into account will be the topics a meetup is about, the topics we're interested in, the events people similar to us are attending, the venues we enjoy going to and anything else we can think of.

== Content based similarity

We're going to start with a content based recommendation to find meetup groups that are similar to one that we like based on having similar topics.

The meetup.com API allows us to extract this data from the `/groups` endpoint.
link:download.py[download.py] contains a Python script which downloads a set of dev related meetup groups into link:data/groups.json[groups.json].
Next we'll use link:https://stedolan.github.io/jq/[jq] to extract the data that we want into CSV files so that we can easily import into Neo4j using the link:http://neo4j.com/docs/stable/query-load-csv.html[LOAD CSV] command.
link:csvify.sh[csvify.sh] contains the code to do this.

Ok.
Now we're ready to get the data into Neo4j.
Let's start by creating nodes to represent each of the meetup groups:

[source,cypher]
----
LOAD CSV WITH HEADERS FROM "file:///Users/markneedham/projects/meetups/modeling-worked-example/data/groups.csv" AS row
CREATE (:Group { id:row.id,
                 name:row.name,
                 urlname:row.urlname,
                 rating:row.rating,
                 link:row.link,
                 created:toint(row.created) })
----

If that worked correctly we should now have 392 nodes in our database.
We'll run the following query to check:

[source, cypher]
----
MATCH (group:Group)
RETURN COUNT(*) AS numberOfGroups
----

Great!
The data is in but it's not much of a graph yet since the groups aren't related to each other in any way.

We're going to connect them together based on the topics that they share, so we'll end up with the following paths in the graph:

[source, text]
----
(group1)-[:HAS_TOPIC]->(topic)<-[:HAS_TOPIC]-(group2)
----

[source, cypher]
----
LOAD CSV WITH HEADERS FROM "file:///Users/markneedham/projects/meetups/modeling-worked-example/data/groups_topics.csv" AS row
MERGE (topic:Topic {id: row.id})
ON CREATE SET topic.name = row.name,
              topic.urlkey = row.urlkey
----

We should really have created an index on `Topic(id)` before running that query otherwise Cypher has to do a `Label Scan` covering more and more nodes on each iteration.
We'll create one on `Group(id)` while we're at it as well.

=== Indexes

Neo4j uses indexes to find the starting points for queries.
They aren't used after those initial starting points are found.

[source,cypher]
----
CREATE INDEX ON :Topic(id)
----

[source,cypher]
----
CREATE INDEX ON :Group(id)
----

We can check the indexes have been created by running the following command in the Neo4j browser:

[source, cypher]
----
:schema
----

Now that we've got those setup let's create some relationships between our topics and groups:

[source, cypher]
----
LOAD CSV WITH HEADERS FROM "file:///Users/markneedham/projects/meetups/modeling-worked-example/data/groups_topics.csv" AS row
MATCH (topic:Topic {id: row.id})
MATCH (group:Group {id: row.groupId})
MERGE (group)-[:HAS_TOPIC]->(topic)
----

What does the graph look like now?

Since we're at the Neo4j meetup let's see which topics are associated with this meetup group.
In an application we'd probably look up the group by `id` but it's much easier to remember the `name` so we're going to create an index on `Group(name)` to help us out:

[source, cypher]
----
CREATE INDEX ON :Group(name)
----

[source,cypher]
----
MATCH (group:Group {name: "Neo4j - London User Group"})-[:HAS_TOPIC]->(topic)
RETURN topic
----

Let's see which other groups have these topics:

[source,cypher]
----
MATCH (group:Group {name: "Neo4j - London User Group"})-[:HAS_TOPIC]->(topic)<-[:HAS_TOPIC]-(otherGroup)
RETURN otherGroup, COUNT(topic) AS topicsInCommon, COLLECT(topic) AS topics
ORDER BY topicsInCommon DESC
LIMIT 10
----

Are there any organisers in common?
Perhaps we really like how well one of the groups is organised and want to find out if that person organises other groups.

[source,cypher]
----
LOAD CSV WITH HEADERS FROM "file:///Users/markneedham/projects/meetups/modeling-worked-example/data/groups.csv" AS row
WITH row WHERE NOT row.organiserMemberId is null
MERGE (member:Member {id: row.organiserMemberId})
ON CREATE SET member.name = row.organiserName
----

[source,cypher]
----
LOAD CSV WITH HEADERS FROM "file:///Users/markneedham/projects/meetups/modeling-worked-example/data/groups.csv" AS row
MATCH (member:Member {id: row.organiserMemberId})
MATCH (group:Group {id: row.id})
MERGE (member)-[:ORGANISES]->(group)
----

== Collaborative Filtering

next we'll introduce some actual people and events into our graph so we can do do more fine grained recommendations of actual events to attend

* How do I know what nodes to create?
* How should I be naming relationships?
* Are my relationships too specific? Are they too generic?
* How can I tell if I've got hidden nodes in my model?
* How do I deal with time?

[source, cypher]
----
CREATE INDEX ON :Member(id)
----

[source, cypher]
----
LOAD CSV WITH HEADERS FROM "file:///Users/markneedham/projects/meetups/modeling-worked-example/data/members.csv" AS row
WITH DISTINCT row.id AS id, row.name AS name
MERGE (member:Member {id: id})
ON CREATE SET member.name = name
----

Now we'll connect the members to the topics and groups that they're interested in:

[source, cypher]
----
LOAD CSV WITH HEADERS FROM "file:///Users/markneedham/projects/meetups/modeling-worked-example/data/members.csv" AS row
WITH row WHERE NOT row.joined is null
MATCH (member:Member {id: row.id})
MATCH (group:Group {id: row.groupId})
MERGE (member)-[:MEMBER_OF {joined: toint(row.joined)}]->(group)
----

[source, cypher]
----
USING PERIODIC COMMIT 10000
LOAD CSV WITH HEADERS FROM "file:///Users/markneedham/projects/meetups/modeling-worked-example/data/members.csv" AS row
WITH split(row.topics, ";") AS topics, row.id AS memberId
UNWIND topics AS topicId
MATCH (member:Member {id: memberId})
MATCH (topic:Topic {id: topicId})
MERGE (member)-[:INTERESTED_IN]->(topic)
----

What does the graph look like now?

what meetup groups might be interesting to me?

[source, cypher]
----
CREATE INDEX ON :Member(name)
----

[source, cypher]
----
MATCH (member:Member {name: "Mark Needham"})-[:INTERESTED_IN]->(topic)<-[:HAS_TOPIC]-(group)
RETURN group.name, COUNT(*) AS numberOfTopics, COLLECT(topic.name) AS topics
ORDER BY numberOfTopics DESC
----

That's fine but maybe I'm already a member of most of those groups.
The following query filters out any groups I'm already a member of:

[source,cypher]
----
MATCH (member:Member {name: "Mark Needham"})-[:INTERESTED_IN]->(topic)<-[:HAS_TOPIC]-(group)
WHERE NOT (member)-[:MEMBER_OF]->(group)
RETURN group.name, COUNT(*) AS numberOfTopics, COLLECT(topic.name) AS topics
ORDER BY numberOfTopics DESC
----

Let's say this week I'm particularly interested in learning about `Machine Learning` so I want to weight in favour of groups that focus on that topic.

[source, cypher]
----
MATCH (member:Member {name: "Mark Needham"})-[:INTERESTED_IN]->(topic)<-[:HAS_TOPIC]-(group)
WHERE NOT (member)-[:MEMBER_OF]->(group)
WITH group, COUNT(*) AS numberOfTopics, COLLECT(topic.name) AS topics
WITH group, topics, REDUCE(score = 0, topic in topics | CASE WHEN topic = "Machine Learning" THEN score+5 ELSE score+1 END) AS score
RETURN group.name, score, topics
ORDER BY score DESC
----

this doesn't help much as everyone uses the same topics...

so the next thing we might want to do is bring events into the mix

[source,cypher]
----
CREATE INDEX ON :Event(id)
----

[source, cypher]
----
LOAD CSV WITH HEADERS FROM "file:///Users/markneedham/projects/meetups/modeling-worked-example/data/events.csv" AS row
MERGE (event:Event {id: row.id})
ON CREATE SET event.name = row.name,
              event.time = toint(row.time),
              event.utcOffset = toint(row.utc_offset)
----

[source, cypher]
----
LOAD CSV WITH HEADERS FROM "file:///Users/markneedham/projects/meetups/modeling-worked-example/data/events.csv" AS row
MATCH (group:Group {id: row.group_id})
MATCH (event:Event {id: row.id})
MERGE (group)-[:HOSTED_EVENT]->(event)
----

Once we've got events we can make recommendations for particular events that people might like to attend
but how will we know which ones are interesting? We need RSVPs for that?

[source, cypher]
----
LOAD CSV WITH HEADERS FROM "file:///Users/markneedham/projects/meetups/modeling-worked-example/data/rsvps.csv" AS row
MATCH (member:Member {id: row.member_id})
MATCH (event:Event {id: row.event_id})
MERGE (member)-[rsvp:RSVPD {id: row.rsvp_id}]->(event)
ON CREATE SET rsvp.created = toint(row.created), rsvp.lastModified = toint(row.mtime), rsvp.response = row.response
----

facts and compositions that exist between entities become clear as we extend the model.
why do we have RSVP as a relationship instead of a node?

So we'd start out with RSVP as a relationship because we only want to use it to identify events to recommend.

We can now make use of these RSVP relationships to find events to recommend to our user.

[source,cypher]
----
MATCH (member:Member {name: "Mark Needham"})-[:RSVPD {response: "yes"}]->(event)<-[:HOSTED_EVENT]-(group)
WITH event, group
ORDER BY (event.time + event.utcOffset)
RETURN event.name, (event.time + event.utcOffset) AS time, group.name
----

[source, cypher]
----
MATCH (member:Member {name: "Mark Needham"})-[:RSVPD {response: "yes"}]->(event)<-[:RSVPD {response: "yes"}]-(otherPerson)
WITH otherPerson, COUNT(*) AS eventsInCommon
ORDER BY eventsInCommon DESC
RETURN otherPerson.name, eventsInCommon
LIMIT 10
----

We can recommend at different levels of granularity.
* Groups
* Events
* Other topics to follow?



What order do people join groups in?
  ** could lead to us introducing a membership concept so we can order them
  ** what groups do people most often join immediately after joining this one? Could be a similarity recommendation.

could we make people `FRIENDS` with others who attended the same meetup events as them?
we could then wait that relationship based on whether they attended the same meetups recently, how many meetups they attended together, how likely they are to have met (look at total number of attendees)


what other data do we need to bring into the graph?

* Run a LDA model over the meetups to extract topics for the individual events?
* People + group memberships
  ** good example of where we store two types of relationships
  **  `HAS_MEMBERSHIP` + `MEMBER_OF` - can use count store on the latter
* People + events they've attended
* Events + venues


Start with the simple model and evolve it
Refactoring the model
  migration using Cypher

What groups do people join after this one?

[source,cypher]
----
MATCH (group:Group {name: ""})
----

Refactor to find adjacent memberships

Find the next group that people joined after the Neo4j London group.

[source,cypher]
----
PROFILE
MATCH (group:Group {name: "Neo4j - London User Group"})<-[membership:MEMBER_OF]-(member),
      (member)-[otherMembership:MEMBER_OF]->(otherGroup)
WITH member, membership, otherMembership, otherGroup
WHERE membership.joined < otherMembership.joined
WITH member, membership, COLLECT(otherGroup)[0] AS nextGroup
RETURN nextGroup, COUNT(*) AS times
ORDER BY times DESC
----

Refactoring to membership

* we want to connect memberships to each other
  - can store the amount of time between

But keeping the original model is great for getting the members of the group

[source,cypher]
----
MATCH (group:Group)-[membership:MEMBERSHIP]->(member)
----
